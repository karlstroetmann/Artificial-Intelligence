loadLibrary("termUtilities");
load("queens-csp.stlx");

// This function collects all variables occurring in the formula f.
// To simplify things it is asssumed that f is either an equation or
// a negated equation and that the terms occurring in this equations
// only contain the arithmetical operators.
collectVars := procedure(f) {
    return varsTerm(parseTerm(f));
};
varsTerm := procedure(f) {
    match (f) {
        case v | isVariable(v): return { varName(v) };
        case n | isNumber(n):   return {};
        case lhs + rhs:         return varsTerm(lhs) + varsTerm(rhs);
        case lhs - rhs:         return varsTerm(lhs) + varsTerm(rhs);
        case lhs * rhs:         return varsTerm(lhs) + varsTerm(rhs);
        case lhs / rhs:         return varsTerm(lhs) + varsTerm(rhs);
        case lhs \ rhs:         return varsTerm(lhs) + varsTerm(rhs);
        case lhs % rhs:         return varsTerm(lhs) + varsTerm(rhs);
        case lhs == rhs:        return varsTerm(lhs) + varsTerm(rhs);
        case lhs != rhs:        return varsTerm(lhs) + varsTerm(rhs);
        case !formula:          return varsTerm(formula);
        case lhs && rhs:        return varsTerm(lhs) + varsTerm(rhs);
        case lhs || rhs:        return varsTerm(lhs) + varsTerm(rhs);
        case lhs => rhs:        return varsTerm(lhs) + varsTerm(rhs);
        case lhs <==> rhs:      return varsTerm(lhs) + varsTerm(rhs);
        case lhs <!=> rhs:      return varsTerm(lhs) + varsTerm(rhs);
        default:                abort("syntax error in varsTerm($f$)");
    }
};

////////////////////////////////////////////////////////////////////////////////

// This is a very basic constraints solver that is based on backtracking.
solve := procedure(csp) {
    [Variables, Values, Constrs] := csp;
    Annotated     := { [f, collectVars(f)] : f in Constrs };
    UnaryConstrs  := { [f, V] : [f, V] in Annotated | #V == 1 };
    BinaryConstrs := { [f, V] : [f, V] in Annotated | #V == 2 };
    msg := "Constraints should be either unary or binary!";
    assert(UnaryConstrs + BinaryConstrs == Annotated, msg); 
    ValuesPerVar := { [v, Values] : v in Variables };
    for ([f, V] in UnaryConstrs) {
        var               := arb(V);
        ValuesPerVar[var] := solve_unary(f, var, ValuesPerVar[var]);
    }
    check {
        return bt_search({}, [Variables, ValuesPerVar, BinaryConstrs]);
    }
};
// Given a constraint containing only variable, find the values that satisfy
// this constraint.
solve_unary := procedure(constraint, variable, Values) {
    LegalValues := {};
    for (value in Values) {
        Assignment := { [variable, value] };
        if (eval_constraint(Assignment, constraint, { variable })) {
            LegalValues += { value };
        }
    }
    return LegalValues;
};

// Given a partial assignment of variable, this procedure tries to complete
// this assignment so that the given CSP is solved.
bt_search := procedure(Assignment, csp) {
    [Variables, Values, Constraints] := csp;
    if (#Assignment == #Variables) {
        return Assignment;
    }
    x := most_constrained_variable(Assignment, Values);
    for (val in Values[x]) {
        check {
            if (is_consistent(x, val, Assignment, Constraints)) {
                NewVals := propagate(x, val, Assignment, Constraints, Values);
                csp     := [Variables, NewVals, Constraints];
                return bt_search(Assignment + { [x, val] }, csp);
            }
        }
    }
    backtrack;
};
// Given the current Assignment, select a variable that has not yet been
// assigned a value.
most_constrained_variable := procedure(Assignment, Values) {
    Unassigned := { [x, U] : [x, U] in Values | Assignment[x] == om };
    minSize    := min({ #U : [x, U] in Unassigned });
    return rnd({ x : [x, U] in Unassigned | #U == minSize });
};
propagate := procedure(x, v, Assignment, Constraints, Values) {
    NewAssignment := Assignment + { [x, v] };
    Values[x]     := { v };
    for ([Formula, Vars] in Constraints | x in Vars) {
        y := arb(Vars - { x });  // Assume binary constraints!!!
        if (!(y in domain(NewAssignment))) {
            NewValues := Values[y];
            for (w in Values[y]) {
                A2 := NewAssignment + { [y, w] };
                if (!eval_constraint(A2, Formula, Vars)) {
                    NewValues -= { w };
                }
            }
            if (NewValues == {}) { backtrack; }
            Values[y] := NewValues;
        }
    }
    return Values;
};
// Evaluate a formula under a given Assignment.
//   Assignment: Binary relation assigning values to variables
//   Vars:       Set of variables occurring in Formula
//   Formula:    The formula to evaluate
eval_constraint := procedure(Assignment, Formula, Vars) {
    for (v in Vars) {
        execute("$v$ := $Assignment[v]$;");
    }
    return eval(Formula);
};
// Check, whether the extended assignment
//     Assignment + { [var, value] };
// is consistent with the given constraints.
is_consistent := procedure(var, value, Assignment, Constraints) {
    NewAssignment := Assignment + { [var, value] };
    for ([Formula, Vars] in Constraints | var in Vars) {
        if (Vars <= domain(NewAssignment)) {
            if (!eval_constraint(NewAssignment, Formula, Vars)) {
                return false;
            }
        }
    }
    return true;
};

main := procedure() {
    resetRandom();
    startTotal := now();
    numIterations := 1;
    for (i in [1..numIterations]) {
        lower :=  4;
        upper := 16;
        for (n in [lower .. upper]) {
            csp      := queensCSP(n);   
            start    := now();
            solution := solve(csp);
            stop     := now();
            total    := (stop - start) / 1000.0;
            printBoard(solution);
            print("The computation for the $n$ queens problem took $total$ seconds.");
        }
    }
    stopTotal := now();
    all := (stopTotal - startTotal) / 1000.0;
    print("The total computation took $all$ seconds.");
};
main();

